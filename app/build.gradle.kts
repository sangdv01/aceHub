import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

// Deployment-specific values stay out of the public repo: set them in local.properties (git-ignored),
// as -P gradle properties, or as env vars ACEHUB_CONTROL_WS_URLS / ACEHUB_DEFAULT_CHANNEL.
val localProps = Properties().apply {
    val f = rootProject.file("local.properties")
    if (f.exists()) f.inputStream().use { load(it) }
}
fun acehubCfg(key: String): String =
    (project.findProperty(key) as String?) ?: localProps.getProperty(key)
        ?: System.getenv(key.uppercase().replace('.', '_')) ?: ""

android {
    namespace = "vn.lienson.acesport.g2probe"
    compileSdk = 35

    defaultConfig {
        applicationId = "vn.lienson.acesport.g2probe"
        minSdk = 24
        targetSdk = 34
        versionCode = 22
        versionName = "1.4.4"

        buildConfigField("String", "CONTROL_WS_URLS", "\"${acehubCfg("acehub.controlWsUrls")}\"")
        buildConfigField("String", "DEFAULT_CHANNEL_ID", "\"${acehubCfg("acehub.defaultChannel")}\"")

        ndk {
            abiFilters.addAll(listOf("armeabi-v7a"))
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            signingConfig = signingConfigs.getByName("debug")
        }
        debug {
            isMinifyEnabled = false
            applicationIdSuffix = ""
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        buildConfig = true
        aidl = true
        viewBinding = true
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

dependencies {
    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.constraintlayout:constraintlayout:2.2.0")

    // Network & JSON
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation("com.google.code.gson:gson:2.11.0")

    // Coroutines & Lifecycle
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.8.6")
}
